use super::tok::{self, Tok};
use super::ast::*;
use std::str::FromStr;

#[recursive_ascent]
grammar<'input>(text: &'input str);

CommaList<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    },
};

pub Sql: Sql = {
    <stmt_list: Stmt*> => {
        Sql{<>}
    },
};

Stmt: Stmt = {
    <SystemStmt> ";" => Stmt::System(<>),
    <DatabaseStmt> ";" => Stmt::Database(<>),
    <TableStmt> ";" => Stmt::Table(<>),
    <IndexStmt> ";" => Stmt::Index(<>),
    <AlterStmt> ";" => Stmt::Alter(<>),
};

SystemStmt: SystemStmt = {
    "show" "databases" => SystemStmt::ShowDatabases,
};

DatabaseName = Name;

DatabaseStmt: DatabaseStmt = {
    "create" "database" <db_name: DatabaseName> => DatabaseStmt::CreateDatabase { <> },
    "drop" "database" <db_name: DatabaseName> => DatabaseStmt::DropDatabase { <> },
    "use" <db_name: DatabaseName> => DatabaseStmt::UseDatabase { <> },
    "show" "tables" => DatabaseStmt::ShowTables,
};

TableName = Name;

TableStmt: TableStmt = {
    "create" "table" <tb_name: TableName> "(" <field_list: CommaList<Field>> ")" => TableStmt::CreateTable { <> },
    "drop" "table" <tb_name: TableName> => TableStmt::DropTable { <> },
    "desc" <tb_name: TableName> => TableStmt::Desc { <> },
    "insert" "into" <tb_name: TableName> "values" <value_lists: CommaList<ValueList>> => TableStmt::Insert { <> },
    "delete" "from" <tb_name: TableName> <where_clause: ("where" <WhereClauseList>)?> => TableStmt::Delete { <> },
    "update" <tb_name: TableName> "set" <set_clause: CommaList<SetClause>> <where_clause: ("where" <WhereClauseList>)?> => TableStmt::Update { <> },
    "select" <selector: Selector> "from" <table_list: CommaList<TableName>> <where_clause: ("where" <WhereClauseList>)?> => TableStmt::Select { <> },
};

IndexName = Name;

IndexStmt: IndexStmt = {
    "create" "index" <idx_name: IndexName> "on" <tb_name: TableName> "(" <column_list: CommaList<ColName>> ")" => {
        IndexStmt::CreateIndex { <> }
    },
    "drop" "index" <idx_name: IndexName> "on" <tb_name: TableName> => IndexStmt::DropIndex { <> },
    "alter" "table" <tb_name: TableName> "add" "index" <idx_name: IndexName> "(" <column_list: CommaList<ColName>> ")" => {
        IndexStmt::AlterAddIndex { <> }
    },
    "alter" "table" <tb_name: TableName> "drop" "index" <idx_name: IndexName> => IndexStmt::AlterDropIndex { <> },
};

AlterStmt: AlterStmt = {
    "alter" "table" <tb_name: TableName> "add" <field: ColumnField> => AlterStmt::AddColumn { <> },
    "alter" "table" <tb_name: TableName> "drop" <col_name: ColName> => AlterStmt::DropColumn { <> },
    "alter" "table" <tb_name: TableName> "change" <col_name: ColName> <field: ColumnField> => AlterStmt::ChangeColumn { <> },
    "alter" "table" <tb_name: TableName> "rename" "to" <new_name: TableName> => AlterStmt::RenameTable { <> },
    "alter" "table" <tb_name: TableName> "add" "primary" "key" "(" <column_list: CommaList<ColName>> ")" => AlterStmt::AddPrimaryKey { <> },
    "alter" "table" <tb_name: TableName> "drop" "primary" "key" => AlterStmt::DropPrimaryKey { <> },
    "alter" "table" <tb_name: TableName> "add" "constraint" <pk_name: Name> "primary" "key" "(" <column_list: CommaList<ColName>> ")" => AlterStmt::AddConstraintPrimaryKey { <> },
    "alter" "table" <tb_name: TableName> "drop" "primary" "key" <pk_name: Name> => AlterStmt::DropConstraintPrimaryKey { <> },
    "alter" "table" <tb_name: TableName> "add" "constraint" <fk_name: Name> "foreign" "key" "(" <column_list: CommaList<ColName>> ")" "references" <foreign_tb_name: TableName> "(" <foreign_column_list: CommaList<ColName>> ")" => {
        AlterStmt::AddConstraintForeignKey { <> }
    },
    "alter" "table" <tb_name: TableName> "drop" "foreign" "key" <fk_name: Name> => AlterStmt::DropConstraintForeignKey { <> },
};

ColName = Name;

SetClause: SetClause = {
    <col_name: ColName> "=" <value: Value> => SetClause { <> },
};

WhereClauseList: Vec<WhereClause> = {
    <v:(<WhereClause> "and")*> <e:WhereClause> => {
        let mut v = v;
        v.push(e);
        v
    },
};

Selector: Selector = {
    "*" => Selector::All,
    CommaList<Column> => Selector::Columns(<>),
};

Column: Column = {
    <tb_name: (<TableName> ".")?> <col_name: ColName> => Column{ <> },
};

WhereClause: WhereClause = {
    <col: Column> <op: Op> <expr: Expr> => WhereClause::Comparison { <> },
    <col: Column> "is" <not_null: "not"?> "null" => {
        WhereClause::IsAssert {
            col: col,
            null: not_null.is_none(),
        }
    },
};

Op: Op = {
    "=" => Op::Equal,
    "<>" => Op::NotEqual,
    "<=" => Op::LessEqual,
    ">=" => Op::GreaterEqual,
    "<" => Op::Less,
    ">" => Op::Greater,
};

Expr: Expr = {
    Value => Expr::Value(<>),
    Column => Expr::Column(<>),
};

ColumnField: Field = {
    <col_name: ColName> <ty: Type> <not_null: ("not" "null")?> <default_value: ("default" <Value>)?> => {
        let not_null = not_null.is_some();
        Field::ColumnField { <> }
    },
};

PrimaryKeyField: Field = {
    "primary" "key" "(" <column_list: CommaList<ColName>> ")" => Field::PrimaryKeyField { <> },
};

ForeignKeyField: Field = {
    "foreign" "key" "(" <col_name: ColName> ")" "references" <foreign_tb_name: TableName> "(" <foreign_col_name: ColName> ")" => Field::ForeignKeyField { <> },
};

Field: Field = {
    ColumnField,
    PrimaryKeyField,
    ForeignKeyField,
};

ValueList: Vec<Value> = {
    "(" <CommaList<Value>> ")"
};

Value: Value = {
    "null" => Value::Null,
    "StringLiteralDoubleQuote" => Value::Str(<>.to_owned()),
    "StringLiteralSingleQuote" => Value::Date(<>.to_owned()),
    "+"? <"Integer"> => { Value::Int( <>.to_owned() ) },
    "-" <"Integer"> => { Value::Int( format!("-{}", <>) ) },
    "+"? <"Float"> => { Value::Float( <>.to_owned() ) },
    "-" <"Float"> => { Value::Float( format!("-{}", <>) ) },
};

Type: Type = {
    "int" "(" <"Integer"> ")" => {
        Type::Int(i64::from_str(<>).unwrap())
    },
    "varchar" "(" <"Integer"> ")" => {
        Type::Varchar(i64::from_str(<>).unwrap())
    },
    "numeric" "(" <a:"Integer"> "," <b:"Integer"> ")" => {
        Type::Numeric(i64::from_str(a).unwrap(), i64::from_str(b).unwrap())
    },
    "date" => {
        Type::Date
    },
    "float" => {
        Type::Float
    },
};

Id: Name = {
    "Id" => <>.to_owned(),
    "indexed" => "indexed".to_owned(),
    // TODO fallback
    "abort" => "abort".to_owned(),
    "action" => "action".to_owned(),
    "after" => "after".to_owned(),
    "analyze" => "analyze".to_owned(),
    "asc" => "asc".to_owned(),
    "attach" => "attach".to_owned(),
    "before" => "before".to_owned(),
    "begin" => "begin".to_owned(),
    "by" => "by".to_owned(),
    "cascade" => "cascade".to_owned(),
//    "cast" => "cast".to_owned(),
//    "column" => "column".to_owned(),
    "conflict" => "conflict".to_owned(),
//    "database" => "database".to_owned(),
    "deferred" => "deferred".to_owned(),
    "desc" => "desc".to_owned(),
    "detach" => "detach".to_owned(),
    "each" => "each".to_owned(),
    "end" => "end".to_owned(),
    "exclusive" => "exclusive".to_owned(),
    "explain" => "explain".to_owned(),
    "fail" => "fail".to_owned(),
    "for" => "for".to_owned(),
    "ignore" => "ignore".to_owned(),
    "immediate" => "immediate".to_owned(),
    "initially" => "initially".to_owned(),
    "instead" => "instead".to_owned(),
//    "like" => "like".to_owned(),
//    "match" => "match".to_owned(),
    "no" => "no".to_owned(),
    "plan" => "plan".to_owned(),
    "query" => "query".to_owned(),
    "key" => "key".to_owned(),
    "of" => "of".to_owned(),
    "offset" => "offset".to_owned(),
    "pragma" => "pragma".to_owned(),
//    "raise" => "raise".to_owned(),
    "recursive" => "recursive".to_owned(),
    "release" => "release".to_owned(),
    "replace" => "replace".to_owned(),
    "restrict" => "restrict".to_owned(),
    "row" => "row".to_owned(),
    "rollback" => "rollback".to_owned(),
    "savepoint" => "savepoint".to_owned(),
    "temp" => "temp".to_owned(),
    "trigger" => "trigger".to_owned(),
    "vacuum" => "vacuum".to_owned(),
    "view" => "view".to_owned(),
    "virtual" => "virtual".to_owned(),
//    "with" => "with".to_owned(),
    "without" => "without".to_owned(),
//    "except" => "except".to_owned(),
//    "intersect" => "intersect".to_owned(),
//    "union" => "union".to_owned(),
    "reindex" => "reindex".to_owned(),
    "rename" => "rename".to_owned(),
    "if" => "if".to_owned(),
};

IdString: Name = {
    Id,
    // TODO "StringLiteral",
};

Name: Name = {
    Id,
    // "StringLiteral" => <>.to_owned(),
    "cross" => "cross".to_owned(),
    "inner" => "inner".to_owned(),
    "left" => "left".to_owned(),
    "natural" => "natural".to_owned(),
    "outer" => "outer".to_owned(),
    "right" => "right".to_owned(),
};


extern {
    type Location = usize;
    type Error = tok::Error;
    enum Tok<'input> {
        "abort" => Tok::Abort,
        "action" => Tok::Action,
        "add" => Tok::Add,
        "after" => Tok::After,
        "all" => Tok::All,
        "alter" => Tok::Alter,
        "analyze" => Tok::Analyze,
        "and" => Tok::And,
        "as" => Tok::As,
        "asc" => Tok::Asc,
        "attach" => Tok::Attach,
        "autoincrement" => Tok::Autoincr,
        "before" => Tok::Before,
        "begin" => Tok::Begin,
        "between" => Tok::Between,
        "by" => Tok::By,
        "cascade" => Tok::Cascade,
        "case" => Tok::Case,
        "cast" => Tok::Cast,
        "change" => Tok::Change,
        "check" => Tok::Check,
        "collate" => Tok::Collate,
        "column" => Tok::ColumnKw,
        "commit" => Tok::Commit,
        "conflict" => Tok::Conflict,
        "constraint" => Tok::Constraint,
        "create" => Tok::Create,
        "cross" => Tok::Cross,
        "current_date" => Tok::CurrentDate,
        "current_time" => Tok::CurrentTime,
        "current_timestamp" => Tok::CurrentTimestamp,
        "database" => Tok::Database,
        "databases" => Tok::Databases,
        "date" => Tok::Date,
        "default" => Tok::Default,
        "deferrable" => Tok::Deferrable,
        "deferred" => Tok::Deferred,
        "delete" => Tok::Delete,
        "desc" => Tok::Desc,
        "detach" => Tok::Detach,
        "distinct" => Tok::Distinct,
        "drop" => Tok::Drop,
        "each" => Tok::Each,
        "else" => Tok::Else,
        "end" => Tok::End,
        "escape" => Tok::Escape,
        "except" => Tok::Except,
        "exclusive" => Tok::Exclusive,
        "exists" => Tok::Exists,
        "explain" => Tok::Explain,
        "fail" => Tok::Fail,
        "float" => Tok::FloatType,
        "for" => Tok::For,
        "foreign" => Tok::Foreign,
        "from" => Tok::From,
        "full" => Tok::Full,
        "glob" => Tok::Glob,
        "group" => Tok::Group,
        "having" => Tok::Having,
        "if" => Tok::If,
        "ignore" => Tok::Ignore,
        "immediate" => Tok::Immediate,
        "in" => Tok::In,
        "index" => Tok::Index,
        "indexed" => Tok::Indexed,
        "initially" => Tok::Initially,
        "inner" => Tok::Inner,
        "insert" => Tok::Insert,
        "instead" => Tok::Instead,
        "int" => Tok::Int,
        "intersect" => Tok::Intersect,
        "into" => Tok::Into,
        "is" => Tok::Is,
        "isnull" => Tok::IsNull,
        "join" => Tok::Join,
        "key" => Tok::Key,
        "left" => Tok::Left,
        "like" => Tok::Like,
        "limit" => Tok::Limit,
        "match" => Tok::Match,
        "natural" => Tok::Natural,
        "no" => Tok::No,
        "not" => Tok::Not,
        "notnull" => Tok::NotNull,
        "null" => Tok::Null,
        "numeric" => Tok::Numeric,
        "of" => Tok::Of,
        "offset" => Tok::Offset,
        "on" => Tok::On,
        "or" => Tok::Or,
        "order" => Tok::Order,
        "outer" => Tok::Outer,
        "plan" => Tok::Plan,
        "pragma" => Tok::Pragma,
        "primary" => Tok::Primary,
        "query" => Tok::Query,
        "raise" => Tok::Raise,
        "recursive" => Tok::Recursive,
        "references" => Tok::References,
        "regexp" => Tok::Regexp,
        "reindex" => Tok::Reindex,
        "release" => Tok::Release,
        "rename" => Tok::Rename,
        "replace" => Tok::Replace,
        "restrict" => Tok::Restrict,
        "right" => Tok::Right,
        "rollback" => Tok::Rollback,
        "row" => Tok::Row,
        "savepoint" => Tok::Savepoint,
        "select" => Tok::Select,
        "set" => Tok::Set,
        "show" => Tok::Show,
        "table" => Tok::Table,
        "tables" => Tok::Tables,
        "temp" => Tok::Temp,
        "then" => Tok::Then,
        "to" => Tok::To,
        "transaction" => Tok::Transaction,
        "trigger" => Tok::Trigger,
        "union" => Tok::Union,
        "unique" => Tok::Unique,
        "update" => Tok::Update,
        "use" => Tok::Use,
        "using" => Tok::Using,
        "vacuum" => Tok::Vacuum,
        "values" => Tok::Values,
        "varchar" => Tok::Varchar,
        "view" => Tok::View,
        "virtual" => Tok::Virtual,
        "when" => Tok::When,
        "where" => Tok::Where,
        "with" => Tok::With,
        "without" => Tok::Without,

        "StringLiteralDoubleQuote" => Tok::StringLiteralDoubleQuote(<&'input str>),
        "StringLiteralSingleQuote" => Tok::StringLiteralSingleQuote(<&'input str>),
        "Id" => Tok::Id(<&'input str>),
        "Variable" => Tok::Variable(<&'input str>),

        "Blob" => Tok::Blob(<&'input str>),
        "Integer" => Tok::Integer(<&'input str>),
        "Float" => Tok::Float(<&'input str>),

        "&" => Tok::BitAnd,
        "~" => Tok::BitNot,
        "|" => Tok::BitOr,
        "," => Tok::Comma,
        "||" => Tok::Concat,
        "." => Tok::Dot,
        "=" => Tok::Equals,
        ">" => Tok::GreaterThan,
        ">=" => Tok::GreaterEquals,
        "(" => Tok::LeftParen,
        "<<" => Tok::LeftShift,
        "<=" => Tok::LessEquals,
        "<" => Tok::LessThan,
        "-" => Tok::Minus,
        "<>" => Tok::NotEquals,
        "+" => Tok::Plus,
        "%" => Tok::Reminder,
        ")" => Tok::RightParen,
        ">>" => Tok::RightShift,
        ";" => Tok::Semi,
        "/" => Tok::Slash,
        "*" => Tok::Star,
    }
}

